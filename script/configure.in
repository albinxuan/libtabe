dnl configure.in for libtabe
dnl
dnl Process this file with autoconf to produce a configure script.
dnl-----------------------------------------------------------------------
AC_INIT(src/tabe.h)
AC_CONFIG_AUX_DIR(script)
AC_CONFIG_HEADER(config.h:script/config.h.in)
AC_CANONICAL_HOST

dnl-----------------------------------------------------------------------
dnl libtabe configure special command line options
dnl-----------------------------------------------------------------------
AC_ARG_ENABLE(debug,
    [  --enable-debug          enable debug],
    [if test x$enableval = xyes; then
         AC_DEFINE(DEBUG) enable_debug=yes
     fi])

AC_ARG_ENABLE(merge-db,
    [  --enable-merge-db       merge the original db files with the new version.],
    [if test x$enableval = xyes; then
         enable_mergedb=yes
     fi])

AC_ARG_WITH(dbinc,
    [  --with-dbinc=PATH       set the include dir of Berkeley DB2.],
    [if test x$withval != x; then
         db_include="$withval"
     fi])

AC_ARG_WITH(dblib,
    [  --with-dblib=PATH       set the library dir of Berkeley DB2.],
    [if test x$withval != x; then
         db_library="$withval"
     fi])

AC_ARG_WITH(dbbin,
    [  --with-dbbin=PATH       set the binary dir of Berkeley DB2.],
    [if test x$withval != x; then
         db_bin="$withval"
     fi])

if test "$program_prefix" != NONE; then
    program_prefix="$program_prefix/"
else
    program_prefix=
fi
if test "$enable_mergedb" = yes; then
    enable_mergedb='mergedb'
else
    enable_mergedb=
fi
AC_SUBST(program_prefix)
AC_SUBST(enable_mergedb)

dnl-----------------------------------------------------------------------
dnl Update version information.
dnl-----------------------------------------------------------------------
verf='src/version.h'
RELEASE_VER=`awk '{if (/RELEASE_VER/) print $3}' $verf`
CURRENT_VER=`awk '{if (/CURRENT_VER/) print $3}' $verf`
REVISION_VER=`awk '{if (/REVISION_VER/) print $3}' $verf`
AGE_VER=`awk '{if (/AGE_VER/) print $3}' $verf`
AC_SUBST(RELEASE_VER)
AC_SUBST(CURRENT_VER)
AC_SUBST(REVISION_VER)
AC_SUBST(AGE_VER)

dnl-----------------------------------------------------------------------
dnl Check compiler: You can specify in 'CC=<..> ./configure'
dnl-----------------------------------------------------------------------
OLD_CFLAGS=$CFLAGS
AC_PROG_CC
if test "x$GCC" != xyes; then
    compiler_type=

    AC_MSG_CHECKING([ANSI C compiler])
    CFLAGS="$OLD_CFLAGS -Aa"
    AC_TRY_RUN([ int main(){return 0;} ], [ compiler_type=ANSIC ],
	       [ compiler_type=UNKNOWN ], [ cross_compiler=1 ])
    if test $compiler_type = ANSIC; then
	AC_MSG_RESULT([yes])
    else
	AC_MSG_RESULT([no])
    fi
fi
CFLAGS=$OLD_CFLAGS

dnl-----------------------------------------------------------------------
dnl Supply default CFLAGS, if not specified by `CFLAGS=flags ./configure'
dnl-----------------------------------------------------------------------
if test -z "$CFLAGS"; then
    if test "x$GCC" = xyes; then
	optimize_opt=-O2
	CFLAGS='-Wall -fsigned-char'
    else
	optimize_opt=-O
	if test "x$compiler_type" = xANSIC; then
	    CFLAGS=-Aa
	else
	    CFLAGS=
	fi
    fi

    if test "x$enable_debug" = xyes; then
        CFLAGS="$CFLAGS -g"
    else
	CFLAGS="$CFLAGS $optimize_opt"
    fi
fi

dnl-----------------------------------------------------------------------
dnl libtool commands.
dnl-----------------------------------------------------------------------
AM_PROG_LIBTOOL
AC_SUBST(LIBTOOL_DEPS)

dnl-----------------------------------------------------------------------
dnl Checks for programs.
dnl-----------------------------------------------------------------------
AC_PROG_CPP
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB
AC_PROG_INSTALL

${INSTALL} -d testdir > /dev/null 2>&1
if test ! -d testdir; then
    INSTALL="$srcdir/script/install-sh -c"
    AC_SUBST(INSTALL)
fi
rm -rf testdir
AC_MSG_RESULT([checking for install -d: $INSTALL])


dnl-----------------------------------------------------------------------
dnl Checks for host type
dnl-----------------------------------------------------------------------
extra_link_flag=
case "$host_os" in
    linux*)  
	AC_DEFINE(LINUX)
        ;;
    freebsd*)
	AC_DEFINE(FREEBSD)
        ;;
    openbsd*)
	AC_DEFINE(OPENBSD)
        ;;
    netbsd*)
	AC_DEFINE(NETBSD)
	extra_link_flag='-Wl,-R'
	;;
    solaris*)
	AC_DEFINE(SOLARIS)
	;;
    hp*)
	AC_DEFINE(HPUX)
	;;
esac


dnl-----------------------------------------------------------------------
dnl Checks for Berkeley DB2 libraries.
dnl-----------------------------------------------------------------------
if test ! x$db_include = x; then
    db_include="-I$db_include"
    CPP="$CPP $db_include"
fi
if test ! x$db_library = x; then
    if test x$extra_link_flag != x; then
	db_library="-L$db_library $extra_link_flag$db_library"
    else
	db_library="-L$db_library"
    fi
    LDFLAGS="$LDFLAGS $db_library"
fi

for f in db db2; do
    AC_CHECK_LIB($f, db_open,
        [ found=y; db_library="$db_library -l$f" ], [ found=n ])
    test $found = y && break
done
if test $found = n; then
    AC_MSG_RESULT([You need Berkeley DB2 to compile bimsphone module.])
    AC_MSG_RESULT([Please use --with-dblib to configure.])
    exit 1
fi

AC_CHECK_HEADERS(db.h, ,
    [ AC_MSG_RESULT([db.h is not available.])
      AC_MSG_RESULT([Please use --with-dbinc to configure.])
      exit 1 ])

if test -z "$db_bin"; then
    db_bin="$PATH"
else
    db_bin="$PATH:$db_bin"
fi
AC_CHECK_PROG(DB_DUMP, db_dump, yes, no, $db_bin)
AC_CHECK_PROG(DB_LOAD, db_load, yes, no, $db_bin)

AC_SUBST(db_include)
AC_SUBST(db_library)
AC_SUBST(db_bin)
AC_SUBST(DB_DUMP)
AC_SUBST(DB_LOAD)

dnl-----------------------------------------------------------------------
dnl Checks for X11R6 library
dnl-----------------------------------------------------------------------
AC_PATH_X
if test x$x_includes = x; then
    AC_MSG_RESULT([You need X11R6 header files to compile libtabe.])
    AC_MSG_RESULT([Please use --x-includes to configure.])
    exit 1
fi

AC_SUBST(x_includes)
AC_SUBST(x_libraries)
dnl LDFLAGS="$LDFLAGS -L$x_libraries"

dnl-----------------------------------------------------------------------
dnl Checks for header files.
dnl-----------------------------------------------------------------------
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h)

dnl-----------------------------------------------------------------------
dnl Checks for typedefs, structures, and compiler characteristics.
dnl-----------------------------------------------------------------------
AC_C_CONST

dnl-----------------------------------------------------------------------
dnl Checks for library functions.
dnl-----------------------------------------------------------------------
AC_FUNC_MEMCMP
AC_CHECK_FUNCS(strdup strerror strstr)

AC_OUTPUT(Makefile:script/Makefile.in \
	  src/Makefile \
	  src/supports/bims/Makefile \
	  src/util/Makefile \
	  src/tsi-src/Makefile)

